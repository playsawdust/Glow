buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "net.minecrell.licenser:net.minecrell.licenser.gradle.plugin:0.4.1"
	}
}

apply plugin: 'java-library'
//apply plugin: 'maven-publish'

// grumble grumble...
if (!project.getPlugins().hasPlugin('net.minecrell.licenser')) {
	apply plugin: 'net.minecrell.licenser'
}

group = 'com.playsawdust.chipper'
archivesBaseName = 'Glow'
version = '0.0.1'

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
}

def lwjgl(String... modules) {
	def ver = '3.2.4-SNAPSHOT'
	def platforms = [ 'linux', 'macos', 'windows' ]
	
	def rtrn = []
	rtrn.add(dependencies.create("org.lwjgl:lwjgl:${ver}"))
	modules.each { module ->
		rtrn.add(dependencies.create("org.lwjgl:lwjgl-${module}:${ver}"))
	}
	platforms.each { platform ->
		rtrn.add(dependencies.create("org.lwjgl:lwjgl:${ver}:natives-${platform}"))
		modules.each { module ->
			rtrn.add(dependencies.create("org.lwjgl:lwjgl-${module}:${ver}:natives-${platform}"))
		}
	}
	return rtrn
}




repositories {
	mavenCentral()
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
	maven { url 'https://jitpack.io' }
}

dependencies {
	api 'org.slf4j:slf4j-api:1.7.9'
	api 'com.google.guava:guava:28.2-jre'
	api 'com.github.falkreon:Splinter:-SNAPSHOT'
	
	compile lwjgl('glfw', 'openal', 'opengl', 'stb')
	compile 'org.joml:joml:1.9.12'
	compile 'blue.endless:jankson:1.2.0'
}

license {
	header = file('headers/mpl2.txt')
	include '**/*.java'
}

java {
	//withJavadocJar()
	//withSourcesJar()
}

tasks.withType(GenerateModuleMetadata) {
	enabled = false
}
